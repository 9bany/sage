<!DOCTYPE html>

<html>

<head>
    <title>hello-wasm example</title>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/codemirror.min.js"
        integrity="sha512-8RnEqURPUc5aqFEN04aQEiPlSAdE0jlFS/9iGgUyNtwFnSKCXhmB6ZTNl7LnDtDWKabJIASzXrzD0K+LYexU9g=="
        crossorigin="anonymous" referrerpolicy="no-referrer"></script>
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/codemirror.min.css"
        integrity="sha512-uf06llspW44/LZpHzHT6qBOIVODjWtv4MxCricRxkzvopAlSWnTf6hpZTFxuuZcuNE9CBQhqE0Seu1CoRk84nQ=="
        crossorigin="anonymous" referrerpolicy="no-referrer" />
    <script src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/addon/mode/simple.min.js"
        integrity="sha512-CGM6DWPHs250F/m90YZ9NEiEUhd9a4+u8wAzeKC6uHzZbYyt9/e2dLC5BGGB6Y0HtEdZQdSDYjDsoTyNGdMrMA=="
        crossorigin="anonymous" referrerpolicy="no-referrer"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/mode/rust/rust.min.js"
        integrity="sha512-g3Nhw36S0p4ZJQcky87D5M+vZbFvLrgsHWYltUy5IW0zKbvi8GlPRjJSo2CyUyQiU01Ier7u+rBABDs3BawKyQ=="
        crossorigin="anonymous" referrerpolicy="no-referrer"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/mode/clike/clike.min.js"
        integrity="sha512-l8ZIWnQ3XHPRG3MQ8+hT1OffRSTrFwrph1j1oc1Fzc9UKVGef5XN9fdO0vm3nW0PRgQ9LJgck6ciG59m69rvfg=="
        crossorigin="anonymous" referrerpolicy="no-referrer"></script>
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/codemirror/6.65.7/theme/ayu-dark.min.css"
        integrity="sha512-mV3RUXi1gt22jDb4UyRBFhZVFgAIiOfRE6ul+2l1Hcj6glyg6x4xlnjPH+neGm/t6XrFmsMRu4++McQu0asjqg=="
        crossorigin="anonymous" referrerpolicy="no-referrer" />
    <style>
        .CodeMirror {
            border: 1px solid #eee;
            height: 100%;
        }
    </style>
</head>

<body style="height: 100%">
    <script type="module">
        import init, { compile_and_run } from './web.js';
        init();

        var source = CodeMirror.fromTextArea(document.getElementById("source"), {
            mode: "rust",
            lineNumbers: true,
            indentUnit: 4,
            theme: 'ayu-dark',
            autoRefresh: true,
        });
        source.setSize("100%", "40em");
        setInterval(function () {
            document.getElementById("source").value = source.getValue();
            source.refresh()
        }, 50);
        var output = CodeMirror.fromTextArea(document.getElementById("output"), {
            mode: "text/x-csrc",
            lineNumbers: true,
            indentUnit: 4,
            theme: 'ayu-dark',
            autoRefresh: true,
        });
        output.setSize("100%", "100%");
        setInterval(function () {
            if (output.getValue() != document.getElementById("output").value) {
                output.setValue(document.getElementById("output").value);
                output.refresh()
            }
        }, 50);
        function setExampleProgram() {
            console.log(document.getElementById("example").value);
            switch (document.getElementById("example").value) {
                case "hashmap":
                    source.setValue(`// Used to represent a value that may or may not exist
enum Option<T> {
    // Some value
    Some(T),
    // No value
    Nothing
}

struct Entry<K, V> {
    // The key of the entry
    key: K,
    // The value of the entry
    val: V
}

struct HashMap<K, V> {
    // The entries of the hashmap
    entries: &mut Option<Entry<K, V>>,
    // The reserved size in memory of the entries
    count_allocated: Int,
    // The number of occupied entries
    count_occupied: Int
}
                  
def is_some<T>(opt: Option<T>): Bool = match opt {
    of Some(_) => True,
    of Nothing => False
};

def refer<T>(opt: &Option<T>): Option<&T> {
    let inner = opt as &T;
    if let &of Nothing = opt {
        return Option<&T> of Nothing;
    }
    return Option<&T> of Some(inner);
}

def refer_mut<T>(opt: &mut Option<T>): Option<&mut T> {
    let inner = opt as &mut T;
    if let &of Nothing = opt {
        return Option<&mut T> of Nothing;
    }
    return Option<&mut T> of Some(inner);
}

def exit(code: Int): ! {
    print("Exiting with code ", code, "\\n");
    while True {
        // Deref a null pointer to cause a segfault
        let error = *(Null as &Int);
    }
}

def unwrap<T>(opt: Option<T>): T {
    if let of Some(val) = opt {
        return val;
    } else {
        print("Error: Tried to unwrap a None value\\n");
        exit(1);
    }
}
  
// Hash a value
def hash<T>(t: T): Int {
    let size = sizeof(t);
    let ptr = &t as &Int;
    
    let mut hashValue = 5381;
    def left_shift(mut a: Int, mut b: Int): Int = {
        while b {
            a *= 2;
            b -= 1;
        }
        return a;
    }

    for let mut i = 0; i < size; i += 1 {
        let mut value = ptr[i];
        while (value != 0) {
            let digit = value % 10;
            hashValue = (left_shift(hashValue, 5) + hashValue) + digit;
            value /= 10;
        }
    }

    if hashValue < 0 {
        hashValue = -hashValue;
    }
    
    return hashValue;
}

def eq<K>(a: K, b: K): Bool {
    let size = sizeof(a);
    let a_ptr = &a as &Int;
    let b_ptr = &b as &Int;
    
    for let mut i=0; i<size; i+=1 {
        if a_ptr[i] != b_ptr[i] {
            return False;
        }
    }
    return True;
}

def Entry_print<K, V>(self: &Entry<K, V>) {
    print("hash: ", hash<K>(self.key), ", key: ", self.key, ", val: ", self.val);
}

def HashMap_clear<K, V>(self: &mut HashMap<K, V>) {
    for let mut i=0; i < self.count_allocated; i+=1 {
        self.entries[i] = Option<Entry<K, V>> of Nothing;
    }
    self.count_occupied = 0;
}


def HashMap_new<K, V>(): HashMap<K, V> {
    let size = 1000 * sizeof<Option<Entry<K, V>>>();
    let entries = alloc(size) as &mut Option<Entry<K, V>>;
    let mut self = {
        entries = entries,
        count_allocated = 1000,
        count_occupied = 0
    };
    HashMap_clear<K, V>(&mut self);
    return self;
}

def HashMap_get<K, V>(self: &HashMap<K, V>, key: K): Option<&V> {
    let mut index = hash<K>(key) % self.count_allocated;
    while is_some<Entry<K, V>>(self.entries[index]) {
        let entry = unwrap<&Entry<K, V>>(refer<Entry<K, V>>(&(self.entries[index])));
        if (eq<K>(entry.key, key)) {
            return Option<&V> of Some(&(entry.val));
        }
        index = (index + 1) % self.count_allocated;  // Linear probing for collision resolution
    }
    return Option<&V> of Nothing;
}

def HashMap_check_bounds<K, V>(self: &mut HashMap<K, V>) {
    if (self.count_allocated - self.count_occupied) < 100 {
        // Allocate new space
        let old_size = self.count_allocated * sizeof(self.entries[0]);
        let old_entries = self.entries;
        let old_map = {
            entries = old_entries,
            count_occupied = self.count_occupied,
            count_allocated = self.count_allocated
        };

        let new_size = old_size * 2;
        self.count_allocated *= 2;
        self.entries = alloc(new_size) as &Option<Entry<K, V>>;
        HashMap_clear<K, V>(self);

        for let mut i=0; i<old_map.count_allocated; i+=1 {
            if let of Some(old_entry) = old_entries[i] {
                match HashMap_get<K, V>(&old_map, old_entry.key) {
                    of Some(val) => HashMap_put<K, V>(self, old_entry.key, *val),
                    of Nothing => {}
                }
            }
        }
    }
}

def HashMap_put<K, V>(self: &mut HashMap<K, V>, key: K, val: V) {
    let mut index = hash<K>(key) % self.count_allocated;
    while is_some<Entry<K, V>>(self.entries[index]) {
        let entry = unwrap<&mut Entry<K, V>>(refer_mut<Entry<K, V>>(&mut (self.entries[index])));
        if (eq<K>(entry.key, key)) {
            entry.val = val;  // Update value if key already exists
            return ();
        }
        index = (index + 1) % self.count_allocated;  // Linear probing for collision resolution
    }

    self.entries[index] = Option<Entry<K, V>> of Some({
        key = key,
        val = val
    });
    self.count_occupied += 1;
    
    HashMap_check_bounds<K, V>(self);
}

def HashMap_print<K, V>(self: &HashMap<K, V>) {
    let mut count = 0;
    if self.count_occupied == 0 {
        print("Empty\\n");
        return ();
    }
    print("--------------------------------\\n");
    for let mut i=0; i < self.count_allocated; i+=1 {
        if let of Some(entry) = self.entries[i] {
            Entry_print<K, V>(&entry);
            print("\\n");
            count += 1;
        }
    }
    print("--------------------------------\\n");
}

def HashMap_drop<K, V>(self: &HashMap<K, V>) {
    print("Freeing ", self.count_allocated, " HashMap Entries\\n");
    del self.entries;
}

struct Point {
    x: Int,
    y: Int
}

def f(x: Int): Point {
    return {x = x, y = x * x};
}

def putstr(s: &Char) {
    let mut i = 0;
    while s[i] != '\\0' {
        print(s[i]);
        i += 1;
    }
}

def putstrln(s: &Char) {
    let mut i = 0;
    while s[i] != '\\0' {
        print(s[i]);
        i += 1;
    }
    print("\\n");
}

def make_x_str(x_count: Int): &Char {
    let value = alloc((x_count + 32) * sizeof<Char>()) as &mut Char;
    for let mut i=0; i<x_count; i+=1 {
        value[i] = 'x';
    }
    value[x_count] = '\\0';
    return value;
}

const INSERT_COUNT = 100;
const CHECK_COUNT = 200;

let mut map = HashMap_new<Point, &Char>();

print("HashMap before inserting:\\n");
HashMap_print<Point, &Char>(&map);

for let mut i=1; i<INSERT_COUNT; i+=1 {
    let point = f(i);
    let key = point as Point;
    let value = make_x_str(i);
    print("Inserting ", key, " -> ");
    putstrln(value);
    HashMap_put<Point, &Char>(&mut map, key, value);
}

print("HashMap after inserting:\\n");
HashMap_print<Point, &Char>(&map);

for let mut i=1; i<CHECK_COUNT; i+=1 {
    let point = f(i);
    let key = point as Point;
    print("Retrieving ", key, ": ");

    match HashMap_get<Point, &Char>(&map, key) {
        of Some(value) => {
            putstrln(*value);
            del *value;
        },
        of Nothing => print("Nothing\\n")
    }
}

HashMap_drop<Point, &Char>(&map);`);
                    break;
                case "hex-editor":
                    document.getElementById("input").value = `+100 !ee 40.B0=7f
d0=48
d1=65
d2=6c
d3=6c
d4=6f
d5=2c
d6=20
d7=77
d8=6f
d9=72
da=6c
db=64
dc=21
dd=0
r`;
                    source.setValue(`// Used to represent a value that may or may not exist
enum Option<T> {
    // Some value
    Some(T),
    // No value
    Nothing,
}

// A command issued to the hex editor
enum Command {
    // Write some data to memory
    Write {
        // Start address
        start: Int,
        // End address
        end: Option<Int>,
        // Value to write
        value: Int,
    },
    // Examine some data in memory
    Examine {
        // Start address
        start: Int,
        // End address
        end: Option<Int>
    },
    // Clear memory to an optional value (or zero)
    Clear(Option<Int>),
    // Grow memory up to a specific size
    Grow(Int),
    // Shrink memory down to a specific size
    Shrink(Int),
    // Quit the program
    Quit,
    // Print the current memory state
    Report,
    // Print the help message
    Help,
    // Unknown command
    Unknown,
}

// A memory buffer
struct Memory {
    // A pointer to the data in memory
    data: &mut Int,
    // The number of bytes in memory
    length: Int,
    // The number of bytes allocated in memory
    capacity: Int,
}

const HELP_MESSAGE = "HELP
====
Commands:
1. Examine a byte in memory:
    '<address>'           to examine a single byte
    '<address>.<address>' to examine a range of memory

2. Write to a byte in memory:
    '<address>=<value>'           to write a single byte
    '<address>.<address>=<value>' to write a range of memory

3. Clear memory:
    '!'         to clear all memory to zero
    '! <value>' to clear all memory to a specific value

4. Grow or shrink memory:
    '+ <value>' to grow memory to a specific size
    '- <value>' to shrink memory to a specific size

5. Report:
    'r' to print the current memory state
    'h' to print this help message

6. Quit:
    'q' to quit
    EOF to quit
    
All numbers are hexadecimal.";

def main() {
    let mut m = Memory_default();

    let mut is_done = False;
    while !is_done {
        let result = Command_read();
        match result {
            of Examine {
                start,
                end=of Some(end)
            } => {
                for let mut i=start; i<end; i+=1 {
                    print_byte(Memory_get(&mut m, i));
                    print(" ");
                }
                print("\\n");
            },
    
            of Examine {
                start,
                end=of Nothing
            } => {
                print_byte(Memory_get(&mut m, start));
                print("\\n");
            },
    
            of Write {
                start,
                end=of Some(end),
                value
            } => {
                for let mut i=start; i<end; i+=1 {
                    Memory_set(&mut m, i, value);
                }
            },
            
            of Write {
                start,
                end=of Nothing,
                value
            } => {
                Memory_set(&mut m, start, value);
            },
    
            of Clear(of Some(val)) => {
                for let mut i=0; i<m.length; i+=1 {
                    m.data[i] = val;
                }
            },
    
            of Clear(of Nothing) => {
                for let mut i=0; i<m.length; i+=1 {
                    m.data[i] = 0;
                }
            },
    
            of Grow(val) => {
                if val < m.length {
                    print("Cannot grow memory to a smaller size ", val, "\\n");
                } else {
                    Memory_extend(&mut m, val - m.length);
                }
            },
    
            of Shrink(val) => {
                if val > m.length {
                    print("Cannot shrink memory to a greater size ", val, "\\n");
                } else {
                    Memory_shrink(&mut m, m.length - val);
                }
            },
    
            of Quit => {
                is_done = True;
            },
    
            of Help => {
                print(HELP_MESSAGE, "\\n");
            },

            of Report => {
                Memory_print(&m);
            },
    
            _ => {
                print("unknown\\n");
            }
        }

    }
    Memory_free(&m);
}

def is_char_between(ch: Char, start: Char, end: Char): Bool {
    return ch as Int >= start as Int && ch as Int <= end as Int;
}

def digit_to_int(ch: Char): Int {
    if (is_char_between(ch, '0', '9')) {
        return ch as Int - '0' as Int;
    } elif (is_char_between(ch, 'A', 'F')) {
        return ch as Int - 'A' as Int + 10;
    } elif (is_char_between(ch, 'a', 'f')) {
        return ch as Int - 'a' as Int + 10;
    } else {
        return 0;
    }
}

def is_digit(ch: Char): Bool {
    return is_char_between(ch, '0', '9') || is_char_between(ch, 'A', 'F') || is_char_between(ch, 'a', 'f');
}

def read_int(): Int {
    let mut ch = ' ';
    input(&mut ch);
    while ch == ' ' || ch == '\\t' || ch == '\\n' || ch == '\\r' {
        input(&mut ch);
    }

    let mut start = digit_to_int(ch);
    while is_digit(ch) {
        input(&mut ch);
        if is_digit(ch) {
            start = start * 16 + digit_to_int(ch);
        }
    }

    return start;
}

def Command_read(): Command {
    let mut ch = ' ';
    while ch {
        input(&mut ch);
        while ch == ' ' || ch == '\\t' || ch == '\\n' || ch == '\\r' {
            input(&mut ch);
        }

        if ch as Int == -1 {
            return Command of Quit;
        }

        if is_digit(ch) {
            // Parse a hex number
            let mut start = digit_to_int(ch);
            while is_digit(ch) {
                input(&mut ch);
                if is_digit(ch) {
                    start = start * 16 + digit_to_int(ch);
                }
            }

            // Check for a range
            if ch == '.' {
                // Parse another int
                input(&mut ch);
                let mut end = digit_to_int(ch);
                while is_digit(ch) {
                    input(&mut ch);
                    if is_digit(ch) {
                        end = end * 16 + digit_to_int(ch);
                    }
                }

                if end < start {
                    print("Invalid range\\n");
                    return Command of Unknown;
                } else {
                    // Check for an assignment
                    if ch == '\\n' {
                        return Command of Examine {
                            start=start,
                            end=Option<Int> of Some(end)
                        };
                    } elif ch == '=' {
                        let value = read_int();
                        return Command of Write {
                            start=start,
                            end=Option<Int> of Some(end),
                            value=value
                        };
                    } else {
                        return Command of Examine {
                            start=start,
                            end=Option<Int> of Some(end)
                        };
                    }
                }
            } elif ch == '=' {
                let value = read_int();
                return Command of Write {
                    start=start,
                    end=Option<Int> of Nothing,
                    value=value
                };
            } else {
                return Command of Examine {
                    start=start,
                    end=Option<Int> of Nothing
                };
            }
            
            return Command of Unknown;
        } else {
            match ch {
                'h' | 'H' => {
                    return Command of Help;
                },
                'r' | 'R' => {
                    return Command of Report;
                },
    
                'q' | 'Q' | '\\0' => {
                    return Command of Quit;
                },

                '+' => {
                    let value = read_int();
                    if value <= 0 {
                        print("Invalid value\\n");
                        return Command of Unknown;
                    }
                    return Command of Grow(value);
                },

                '-' => {
                    let value = read_int();
                    if value <= 0 {
                        print("Invalid value\\n");
                        return Command of Unknown;
                    }
                    return Command of Shrink(value);
                },

                '\\\\' | '!' => {
                    let value = read_int();
                    return Command of Clear(Option<Int> of Some(value));
                },
    
                _ => {
                    return Command of Unknown;
                }
            }
        }

    }
    return Command of Unknown;
}

def copy<T>(dst: &mut T, src: &T, count: Int) {
    for let mut i=0; i<count; i+=1 {
        dst[i] = src[i];
    }
}

def allocate<T>(count: Int): &mut T {
    return alloc(sizeof<T>() * count) as &mut T;
}

def reallocate<T>(src: &T, old_size: Int, new_size: Int): &T {
    let dst = allocate<T>(new_size);
    copy<T>(dst, src, old_size);
    del src;
    return dst;
}

def Memory_push(m: &mut Memory, value: Int) {
    while (m->length >= m->capacity) {
        m->capacity *= 2;
    }
    m->data = allocate<Int>(m->capacity);
    m->data[m->length] = value;
    m->length += 1;
}

def Memory_extend(m: &mut Memory, count: Int) {
    while (m->length + count >= m->capacity) {
        m->capacity *= 2;
    }
    m->data = reallocate<Int>(m->data, m->length, m->capacity);
    m->length += count;
}

def Memory_pop(m: &mut Memory): Int {
    if (m->length == 0) {
        return 0;
    }
    m->length -= 1;
    return m->data[m->length];
}

def Memory_shrink(m: &mut Memory, count: Int) {
    if (m->length < count) {
        m->length = 0;
    } else {
        m->length -= count;
    }
}

def Memory_index(m: &mut Memory, index: Int): &mut Int {
    if (index >= m->length) {
        Memory_extend(m, index - m->length + 1);
    }
    return &mut (m->data[index]);
}

def Memory_get(m: &mut Memory, index: Int): Int {
    return *(Memory_index(m, index));
}

def Memory_set(m: &mut Memory, index: Int, value: Int) {
    *(Memory_index(m, index)) = value;
}

def Memory_new(n: Int): Memory {
    return {
        data=allocate<Int>(n),
        length=0,
        capacity=n
    };
}

def Memory_default(): Memory {
    return Memory_new(10000);
}

def Memory_free(m: &Memory) {
    del m->data;
}

def print_byte(mut c: Int) {
    c = c & 0xFF;
    let hex = "0123456789ABCDEF";
    print(hex[(c / 16) & 0xF]);
    print(hex[c & 0xF]);
}

def print_addr(mut c: Int) {
    c = c & 0xFFFFFFFF;
    let hex = "0123456789ABCDEF";
    print(hex[(c / 0x10000000) & 0xF]);
    print(hex[(c / 0x1000000) & 0xF]);
    print(hex[(c / 0x100000) & 0xF]);
    print(hex[(c / 0x10000) & 0xF]);
    print(hex[(c / 0x1000) & 0xF]);
    print(hex[(c / 0x100) & 0xF]);
    print(hex[(c / 0x10) & 0xF]);
    print(hex[c & 0xF]);
}

def Memory_print(m: &Memory) {
    print("Memory: ", m->length, "/", m->capacity, "\\n");
    if (m->length == 0) {
        print("Empty\\n");
    }
    for let mut i=0; i<m->length; i+=1 {
        // Print address
        if (i % 16 == 0) {
            print_addr(i);
            print(": ");
        }

        print_byte(m->data[i]);
        print(" ");
        if i % 16 == 15 {
            // print ASCII
            print(" | ");
            for let mut j=i-15; j<=i; j+=1 {
                let c = m->data[j];
                if (c >= 32 && c <= 126) {
                    print(c as Char);
                } else {
                    print(".");
                }
            }
            print("\\n");
        }
    }

    // print ASCII
    if (m->length % 16 != 0) {
        let i = m->length;
        let j = i - (i % 16);
        for let mut k=i; k<j+16; k+=1 {
            print("   ");
        }
        print(" | ");
        for let mut k=j; k<i; k+=1 {
            let c = m->data[k];
            if (c >= 32 && c <= 126) {
                print(c as Char);
            } else {
                print(".");
            }
        }
        print("\\n");
    }
}

main();`);
                    break;
                case "aes":
                    source.setValue(`def print_hex(n: Int, lower: Bool) {
    if n < 10 {
        print((n + '0' as Int) as Char);
    } elif n < 16 {
        print((n - 10 + (lower? 'a' : 'A') as Int) as Char);
    } else {
        print_hex(n / 16, lower);
        print_hex(n % 16, lower);
    }
}

def print_byte(mut n: Int, lower: Bool) {
    n &= 0xFF;
    print_hex(n / 16, lower);
    print_hex(n % 16, lower);
}

def left_shift(mut a: Int, mut b: Int): Int = {
    while b {
        a *= 2;
        b -= 1;
    }
    return a;
}

const MIN_INT = -2147483648;

def right_shift(mut a: Int, mut b: Int): Int = {
    let mut offset = MIN_INT, was_negative = a < 0;
    for let mut i=0; i < b - 1; i += 1 {
        offset /= 2;
    }

    if was_negative { a = ~a; }
    
    while b {
        a /= 2;
        b -= 1;
    }

    if was_negative { a = ~a ^ offset; }

    return a;
}

def print_hexln(n: Int, lower: Bool) {
    print_hex(n, lower);
    print("\\n");
}

def print_byteln(n: Int, lower: Bool) {
    print_byte(n, lower);
    print("\\n");
}

def xtime(mut n: Int): Int {
    n &= 0xFF;
    n *= 2;

    return n < 0x100? n : n ^ 0x1B;
}

def multiply(mut a: Int, mut b: Int): Int {
    a &= 0xFF;
    b &= 0xFF;
    let mut result = 0;
    let mut next_term = a;
    for let mut i=0; i < 8; i += 1 {
        if b & 1 {
            result ^= next_term;
        }
        next_term = xtime(next_term);
        b /= 2;
    }

    return result & 0xFF;
}

type State = [Int * 16];

def row_major(c0r0: Int, c1r0: Int, c2r0: Int, c3r0: Int,
    c0r1: Int, c1r1: Int, c2r1: Int, c3r1: Int,
    c0r2: Int, c1r2: Int, c2r2: Int, c3r2: Int,
    c0r3: Int, c1r3: Int, c2r3: Int, c3r3: Int): State = [
    c0r0, c0r1, c0r2, c0r3,
    c1r0, c1r1, c1r2, c1r3,
    c2r0, c2r1, c2r2, c2r3,
    c3r0, c3r1, c3r2, c3r3
];

def col_major(c0r0: Int, c0r1: Int, c0r2: Int, c0r3: Int,
    c1r0: Int, c1r1: Int, c1r2: Int, c1r3: Int,
    c2r0: Int, c2r1: Int, c2r2: Int, c2r3: Int,
    c3r0: Int, c3r1: Int, c3r2: Int, c3r3: Int): State = [
    c0r0, c0r1, c0r2, c0r3,
    c1r0, c1r1, c1r2, c1r3,
    c2r0, c2r1, c2r2, c2r3,
    c3r0, c3r1, c3r2, c3r3
];

def get(state: &State, row: Int, col: Int): Int {
    return (state as &Int)[row * 4 + col];
}

def set(state: &mut State, row: Int, col: Int, value: Int) {
    (state as &mut Int)[row * 4 + col] = value;
}

def print_state(self: &State) {
    for let mut i=0; i < 4; i += 1 {
        for let mut j=0; j < 4; j += 1 {
            print_byte(get(self, i, j), False);
            print(" ");
        }
        print("\\n");
    }
}

def rotate_row(self: &mut State, row: Int, steps: Int) {
    for let mut i=0; i<steps; i += 1 {
        let temp = get(self, row, 0);
        set(self, row, 0, get(self, row, 1));
        set(self, row, 1, get(self, row, 2));
        set(self, row, 2, get(self, row, 3));
        set(self, row, 3, temp);
    }
}

def shift_rows(self: &mut State) {
    rotate_row(self, 1, 1);
    rotate_row(self, 2, 2);
    rotate_row(self, 3, 3);
}

def inverse_shift_rows(self: &mut State) {
    rotate_row(self, 1, 3);
    rotate_row(self, 2, 2);
    rotate_row(self, 3, 1);
}

def add_round_key(self: &mut State, round_key: &State) {
    let round_key_ptr = round_key as &Int;
    let self_ptr = self as &mut Int;
    for let mut i=0; i < 16; i += 1 {
        self_ptr[i] ^= round_key_ptr[i];
    }
}

def mix_columns(self: &mut State) {
    for let mut j=0; j < 4; j += 1 {
        let s0 = get(self, 0, j);
        let s1 = get(self, 1, j);
        let s2 = get(self, 2, j);
        let s3 = get(self, 3, j);

        set(self, 0, j, multiply(0x02, s0) ^ multiply(0x03, s1) ^ s2 ^ s3);
        set(self, 1, j, s0 ^ multiply(0x02, s1) ^ multiply(0x03, s2) ^ s3);
        set(self, 2, j, s0 ^ s1 ^ multiply(0x02, s2) ^ multiply(0x03, s3));
        set(self, 3, j, multiply(0x03, s0) ^ s1 ^ s2 ^ multiply(0x02, s3));
    }
}

def inverse_mix_columns(self: &mut State) {
    for let mut j=0; j < 4; j += 1 {
        let s0 = get(self, 0, j);
        let s1 = get(self, 1, j);
        let s2 = get(self, 2, j);
        let s3 = get(self, 3, j);

        set(self, 0, j, multiply(0x0e, s0) ^ multiply(0x0b, s1) ^ multiply(0x0d, s2) ^ multiply(0x09, s3));
        set(self, 1, j, multiply(0x09, s0) ^ multiply(0x0e, s1) ^ multiply(0x0b, s2) ^ multiply(0x0d, s3));
        set(self, 2, j, multiply(0x0d, s0) ^ multiply(0x09, s1) ^ multiply(0x0e, s2) ^ multiply(0x0b, s3));
        set(self, 3, j, multiply(0x0b, s0) ^ multiply(0x0d, s1) ^ multiply(0x09, s2) ^ multiply(0x0e, s3));
    }
}


def substitute_byte(mut byte: Int): Int {
    let S_BOX = [[99, 124, 119, 123, 242, 107, 111, 197, 48, 1, 103, 43, 254, 215, 171, 118], [202, 130, 201, 125, 250, 89, 71, 240, 173, 212, 162, 175, 156, 164, 114, 192], [183, 253, 147, 38, 54, 63, 247, 204, 52, 165, 229, 241, 113, 216, 49, 21], [4, 199, 35, 195, 24, 150, 5, 154, 7, 18, 128, 226, 235, 39, 178, 117], [9, 131, 44, 26, 27, 110, 90, 160, 82, 59, 214, 179, 41, 227, 47, 132], [83, 209, 0, 237, 32, 252, 177, 91, 106, 203, 190, 57, 74, 76, 88, 207], [208, 239, 170, 251, 67, 77, 51, 133, 69, 249, 2, 127, 80, 60, 159, 168], [81, 163, 64, 143, 146, 157, 56, 245, 188, 182, 218, 33, 16, 255, 243, 210], [205, 12, 19, 236, 95, 151, 68, 23, 196, 167, 126, 61, 100, 93, 25, 115], [96, 129, 79, 220, 34, 42, 144, 136, 70, 238, 184, 20, 222, 94, 11, 219], [224, 50, 58, 10, 73, 6, 36, 92, 194, 211, 172, 98, 145, 149, 228, 121], [231, 200, 55, 109, 141, 213, 78, 169, 108, 86, 244, 234, 101, 122, 174, 8], [186, 120, 37, 46, 28, 166, 180, 198, 232, 221, 116, 31, 75, 189, 139, 138], [112, 62, 181, 102, 72, 3, 246, 14, 97, 53, 87, 185, 134, 193, 29, 158], [225, 248, 152, 17, 105, 217, 142, 148, 155, 30, 135, 233, 206, 85, 40, 223], [140, 161, 137, 13, 191, 230, 66, 104, 65, 153, 45, 15, 176, 84, 187, 22]];
    byte &= 0xFF;
    let row = right_shift(byte, 4) & 0xF;
    let col = byte & 0xF;
    return S_BOX[row][col];
}

def inverse_substitute_byte(mut byte: Int): Int {
    let INVERSE_S_BOX = [[82, 9, 106, 213, 48, 54, 165, 56, 191, 64, 163, 158, 129, 243, 215, 251], [124, 227, 57, 130, 155, 47, 255, 135, 52, 142, 67, 68, 196, 222, 233, 203], [84, 123, 148, 50, 166, 194, 35, 61, 238, 76, 149, 11, 66, 250, 195, 78], [8, 46, 161, 102, 40, 217, 36, 178, 118, 91, 162, 73, 109, 139, 209, 37], [114, 248, 246, 100, 134, 104, 152, 22, 212, 164, 92, 204, 93, 101, 182, 146], [108, 112, 72, 80, 253, 237, 185, 218, 94, 21, 70, 87, 167, 141, 157, 132], [144, 216, 171, 0, 140, 188, 211, 10, 247, 228, 88, 5, 184, 179, 69, 6], [208, 44, 30, 143, 202, 63, 15, 2, 193, 175, 189, 3, 1, 19, 138, 107], [58, 145, 17, 65, 79, 103, 220, 234, 151, 242, 207, 206, 240, 180, 230, 115], [150, 172, 116, 34, 231, 173, 53, 133, 226, 249, 55, 232, 28, 117, 223, 110], [71, 241, 26, 113, 29, 41, 197, 137, 111, 183, 98, 14, 170, 24, 190, 27], [252, 86, 62, 75, 198, 210, 121, 32, 154, 219, 192, 254, 120, 205, 90, 244], [31, 221, 168, 51, 136, 7, 199, 49, 177, 18, 16, 89, 39, 128, 236, 95], [96, 81, 127, 169, 25, 181, 74, 13, 45, 229, 122, 159, 147, 201, 156, 239], [160, 224, 59, 77, 174, 42, 245, 176, 200, 235, 187, 60, 131, 83, 153, 97], [23, 43, 4, 126, 186, 119, 214, 38, 225, 105, 20, 99, 85, 33, 12, 125]];
    byte &= 0xFF;
    let row = right_shift(byte, 4) & 0xF;
    let col = byte & 0xF;
    return INVERSE_S_BOX[row][col];
}

def substitute_bytes(self: &mut State) {
    for let mut i=0; i < 4; i += 1 {
        for let mut j=0; j < 4; j += 1 {
            set(self, i, j, substitute_byte(get(self, i, j)));
        }
    }
}

def inverse_substitute_bytes(self: &mut State) {
    for let mut i=0; i < 4; i += 1 {
        for let mut j=0; j < 4; j += 1 {
            set(self, i, j, inverse_substitute_byte(get(self, i, j)));
        }
    }
}

enum Key {
    Key128([Int * 16]),
    Key192([Int * 24]),
    Key256([Int * 32]),
}

def get_key_rounds(self: &Key): Int {
    match self {
        &of Key128(_) => 10,
        &of Key192(_) => 12,
        &of Key256(_) => 14,
        _ => 0
    }
}

def get_key_length(self: &Key): Int {
    match self {
        &of Key128(_) => 4,
        &of Key192(_) => 6,
        &of Key256(_) => 8,
        _ => 0
    }
}

def get_key_size(self: &Key): Int {
    return get_key_length(self) * 4;
}

def get_key_data(key: &Key): &Int = key as &Int;

def print_key(self: &Key) {
    match self {
        &of Key128(key) => print("Key128: "),
        &of Key192(key) => print("Key192: "),
        &of Key256(key) => print("Key256: "),
        _ => print("Unknown key type: "),
    }
    let size = get_key_size(self);
    let data = get_key_data(self);
    for let mut i=0; i < size; i+=1 {
        print_byte(data[i], False);
    }
}

def print_keyln(self: &Key) {
    print_key(self);
    print("\\n");
}


def substitute_word(word: Int): Int {
    let byte0 = substitute_byte(right_shift(word, 24) & 0xFF);
    let byte1 = substitute_byte(right_shift(word, 16) & 0xFF);
    let byte2 = substitute_byte(right_shift(word, 8) & 0xFF);
    let byte3 = substitute_byte(word & 0xFF);
    return left_shift(byte0, 24) + left_shift(byte1, 16) + left_shift(byte2, 8) + byte3;
}

def rotate_word(word: Int): Int {
    let byte0 = right_shift(word, 24) & 0xFF;
    let byte1 = right_shift(word, 16) & 0xFF;
    let byte2 = right_shift(word, 8) & 0xFF;
    let byte3 = word & 0xFF;
    return left_shift(byte1, 24) + left_shift(byte2, 16) + left_shift(byte3, 8) + byte0;
}

def word(byte3: Int, byte2: Int, byte1: Int, byte0: Int): Int {
    return left_shift(byte3, 24) + left_shift(byte2, 16) + left_shift(byte1, 8) + byte0;
}

def bytes(word: Int): [Int * 4] {
    let byte0 = right_shift(word, 24) & 0xFF;
    let byte1 = right_shift(word, 16) & 0xFF;
    let byte2 = right_shift(word, 8) & 0xFF;
    let byte3 = word & 0xFF;
    return [byte0, byte1, byte2, byte3];
}

def round_key(word0: Int, word1: Int, word2: Int, word3: Int): State {
    let bytes0 = bytes(word0);
    let bytes1 = bytes(word1);
    let bytes2 = bytes(word2);
    let bytes3 = bytes(word3);
    return col_major(bytes0[0], bytes1[0], bytes2[0], bytes3[0],
              bytes0[1], bytes1[1], bytes2[1], bytes3[1],
              bytes0[2], bytes1[2], bytes2[2], bytes3[2],
              bytes0[3], bytes1[3], bytes2[3], bytes3[3]);
}

def key_expansion(key: &Key, nr: Int, nk: Int): &Int = {
    let key_data = key as &Int;
    let w = alloc(4 * (nr + 1) * sizeof<Int>()) as &mut Int;
    for let mut i=0; i < nk; i+=1 {
        w[i] = word(key_data[4*i], key_data[4*i+1], key_data[4*i+2], key_data[4*i+3]);
    }

    let rcon = [1, 2, 4, 8, 16, 32, 64, 128, 27, 54];
    for let mut i=nk; i < 4 * (nr + 1); i += 1 {
        let mut temp = w[i-1];
        if i % nk == 0 {
            temp = substitute_word(rotate_word(temp)) ^ word(rcon[i / nk - 1], 0, 0, 0);
        } elif nk == 8 && i % nk == 4 {
            temp = substitute_word(temp);
        }

        w[i] = w[i-nk] ^ temp;
    }

    return w;
}

def cipher(self: &mut State, key: &Key) {
    let len = get_key_length(key);
    let nr = get_key_rounds(key);
    let k = key_expansion(key, nr, len);
    let mut rk = round_key(k[0], k[1], k[2], k[3]);
    add_round_key(self, &rk);
    for let mut i=4; i < 4 * (nr + 1); i+=4 {
        rk = round_key(k[i], k[i+1], k[i+2], k[i+3]);
        substitute_bytes(self);
        shift_rows(self);
        if i < 4 * nr {
            mix_columns(self);
        }
        add_round_key(self, &rk);
    }

    del k;
}

def inverse_cipher(self: &mut State, key: &Key) {
    let len = get_key_length(key);
    let nr = get_key_rounds(key);
    let k = key_expansion(key, nr, len);

    let mut rk = round_key(k[4*nr], k[4*nr+1], k[4*nr+2], k[4*nr+3]);
    add_round_key(self, &rk);
    for let mut i=4*nr-4; i >= 0; i-=4 {
        rk = round_key(k[i], k[i+1], k[i+2], k[i+3]);
        inverse_shift_rows(self);
        inverse_substitute_bytes(self);
        add_round_key(self, &rk);
        if i > 0 {
            inverse_mix_columns(self);
        }
    }

    del k;
}
let key = Key of Key128 [
    0x00, 0x01, 0x02, 0x03,
    0x04, 0x05, 0x06, 0x07,
    0x08, 0x09, 0x0A, 0x0B,
    0x0C, 0x0D, 0x0E, 0x0F
];


let mut state: State = row_major(
    0x00, 0x01, 0x02, 0x03,
    0x04, 0x05, 0x06, 0x07,
    0x08, 0x09, 0x0A, 0x0B,
    0x0C, 0x0D, 0x0E, 0x0F
);

print_keyln(&key);
print("Raw\\n");
print_state(&state);
print("\\n");
print("Cipher\\n");
cipher(&mut state, &key);
print_state(&state);
print("\\n");
print("Inverse-Cipher\\n");
inverse_cipher(&mut state, &key);
print_state(&state);
print("\\n");`);
                    break;
                case "list":
                    source.setValue(`enum List<T> {
    Nil,
    Cons {
        data: T,
        next: &List<T>
    }
}

def print_list<T>(l: &List<T>) {
    match *l {
        of Nil => {},
        of Cons {data, next=&of Nil} => {
            print(data);
        },
        of Cons {data, next} => {
            print(data, ", ");
            print_list<T>(next);
        },
        _ => {}
    }
}

def make_list<T, U>(cons: Int -> T, len: Int): List<T> {
    if len == 0 {
        return List<T> of Nil;
    } else {
        return List<T> of Cons {
            data = cons(len),
            next = new make_list<T, U>(cons, len - 1)
        };
    }
}

def identity<T>(t: T): T = t;

let list1 = make_list<Int, Int>(identity<Int>, 100);
print_list<Int>(&list1);
print("\\n");

print_list<Int>(new List<Int> of Cons {
    data = 1,
    next = new List<Int> of Cons {
        data = 2,
        next = new List<Int> of Cons {
            data = 3,
            next = new List<Int> of Cons {
                data = 4,
                next = new List<Int> of Nil
            }
        }
    }
});
print("\\n");

let x = new List<Char> of Cons {
    data = 'a',
    next = new List<Char> of Cons {
        data = 'b',
        next = new List<Char> of Cons {
            data = 'c',
            next = new List<Char> of Cons {
                data = 'd',
                next = new List<Char> of Nil
            }
        }
    }
};

print_list<Char>(x);
print("\\n");
`)
                    break;
                case "result":
                    source.setValue(`enum Result<T, E> {
    Ok(T),
    Err(E)
}

def print_result<T, E>(res: Result<T, E>) {
    match res {
        of Ok(x) => print("success: ", x, "\\n"),
        of Err(e) => print("error: ", e, "\\n")
    }
}

def map<T, U, E>(f: T -> U, res: Result<T, E>): Result<U, E> {
    match res {
        of Ok(x) => Result<U, E> of Ok(f(x)),
        of Err(e) => Result<U, E> of Err(e)
    }
}

def map_err<T, E, F>(f: E -> F, res: Result<T, E>): Result<T, F> {
    match res {
        of Ok(x) => Result<T, F> of Ok(x),
        of Err(e) => Result<T, F> of Err(f(e))
    }
}

def and_then<T, U, E>(f: T -> Result<U, E>, res: Result<T, E>): Result<U, E> {
    match res {
        of Ok(x) => f(x),
        of Err(e) => Result<U, E> of Err(e)
    }
}

def or_else<T, E, F>(f: E -> Result<T, F>, res: Result<T, E>): Result<T, F> {
    match res {
        of Ok(x) => Result<T, F> of Ok(x),
        of Err(e) => f(e)
    }
}

def inc(x: Int): Int = x + 1;
def to_float(n: Int): Float = n as Float;
def half(n: Float): Float = n / 2;

let text = "hello";
print("Should print 5 / 2: ");
print_result<Float, &Char>(map<Float, Float, &Char>(half, map<Int, Float, &Char>(to_float, Result<Int, &Char> of Ok 5)));
`);
                    break;

                case "vector":
                    source.setValue(`enum Option<T> {
    Some(T),
    Nothing
}

enum Result<T, E> {
    Ok(T),
    Err(E)
}

def allocate<T>(count: Int): Option<&mut T> {
    if (count <= 0) {
        return Option<&mut T> of Nothing;
    }
    let ptr = alloc(count * sizeof<T>()) as &mut T;
    if (ptr == Null) {
        return Option<&mut T> of Nothing;
    } else {
        return Option<&mut T> of Some(ptr);
    }
}

def copy_slice<T>(data: &T, count: Int): Option<&mut T> {
    match allocate<T>(count) {
        of Some(mut ptr) => {
            for let mut i = 0; i < count; i+=1 {
                ptr[i] = data[i];
            }
            return Option<&mut T> of Some(ptr);
        },
        of Nothing => {
            return Option<&mut T> of Nothing;
        }
    }
}

def copy<T>(data: &T): Option<&mut T> {
    return copy_slice<T>(data, 1);
}

def reallocate<T>(data: &T, old_count: Int, new_count: Int): Option<&T> {
    match allocate<T>(new_count) {
        of Some(ptr) => {
            for let mut i = 0; i < old_count; i+=1 {
                ptr[i] = data[i];
            }
            del data;
            return Option<&T> of Some(ptr);
        },
        of Nothing => {
            return Option<&T> of Nothing;
        }
    }
}

enum Either<T> {
    Left(T),
    Right(T)
}

enum Ordering {
    Less,
    Equal,
    Greater
}

struct String {
    data: &mut Char,
    len: Int,
    cap: Int
}

def String_clone(s: &String): String {
    let data = alloc(s->cap) as &mut Char;
    for let mut i = 0; i < s->len; i+=1 {
        data[i] = s->data[i];
    }
    return {
        data=data,
        len=s->len,
        cap=s->cap
    };
}

def String_new_with_capacity(cap: Int): Option<String> {
    match allocate<Char>(cap) {
        of Some(data) => {
            return Option<String> of Some({
                data=data,
                len=0,
                cap=cap
            });
        },
        of Nothing => {
            return Option<String> of Nothing;
        }
    }
}

def String_from(text: &Char): Option<String> {
    let mut len = 0;
    while (text[len] != '\\0') {
        len += 1;
    }
    match String_new_with_capacity(len) {
        of Some(mut s) => {
            for let mut i = 0; i < len; i+=1 {
                s.data[i] = text[i];
            }
            s.len = len;
            return Option<String> of Some(s);
        },
        of Nothing => {
            return Option<String> of Nothing;
        }
    }
}

def String_print(s: &String) {
    for let mut i = 0; i < s->len; i+=1 {
        print(s->data[i]);
    }
}

def nth(s: &String, n: Int): Option<&Char> {
    if (n < 0 || n >= s->len) {
        return Option<&Char> of Nothing;
    } else {
        return Option<&Char> of Some(&(s->data[n]));
    }
}

def String_push(mut s: String, c: Char): String {
    if s.len == s.cap {
        let new_cap = s.cap * 2;
        match reallocate<Char>(s.data, s.cap, new_cap) {
            of Some(data) => {
                s.data = data;
                s.cap = new_cap;
            },
            of Nothing => {
                return s;
            }
        }
    }
    s.data[s.len] = c;
    s.len += 1;
    return s;
}

def String_drop(s: &String) {
    if s->data != Null {
        del s->data;
    }
}

struct Sequence<T> {
    len: Int,
    cap: Int,
    data: &mut T,
}

def Sequence_clone<T>(s: &Sequence<T>): Option<Sequence<T>> {
    match copy_slice<T>(s->data, s->cap) {
        of Some(data) => {
            return Option<Sequence<T>> of Some({
                data=data,
                len=s->len,
                cap=s->cap
            });
        },
        of Nothing => {
            return Option<Sequence<T>> of Nothing;
        }
    }
}

def Sequence_new_with_capacity<T>(cap: Int): Option<Sequence<T>> {
    match allocate<T>(cap) {
        of Some(data) => {
            return Option<Sequence<T>> of Some({
                data=data,
                len=0,
                cap=cap
            });
        },
        of Nothing => {
            return Option<Sequence<T>> of Nothing;
        }
    }
}

def Sequence_from<T>(data: &T, len: Int): Option<Sequence<T>> {
    match Sequence_new_with_capacity<T>(len) {
        of Some(mut s) => {
            for let mut i = 0; i < len; i+=1 {
                s.data[i] = data[i];
            }
            s.len = len;
            return Option<Sequence<T>> of Some(s);
        },
        of Nothing => {
            return Option<Sequence<T>> of Nothing;
        }
    }
}

def Sequence_print<T>(s: &Sequence<T>) {
    print("[");
    for let mut i = 0; i < s->len; i+=1 {
        print(s->data[i]);
        if i < s->len - 1 {
            print(", ");
        }
    }
    print("]");
}

def Sequence_println<T>(s: &Sequence<T>) {
    Sequence_print<T>(s);
    print("\\n");
}

def Sequence_push<T>(s: Sequence<T>, c: T): Sequence<T> {
    if (s.len == s.cap) {
        let old_data = s.data;
        if let of Some(ptr) = reallocate<T>(s.data, s.cap, s.cap * 2) {
            s.data = ptr;
        } else {
            return s;
        }
        s.cap *= 2;
    }
    s.data[s.len] = c;
    s.len += 1;
    return s;
}

def Sequence_concat<T>(first: Sequence<T>, second: Sequence<T>): Sequence<T> {
    let new_len = first.len + second.len;
    let new_cap = first.cap + second.cap;
    let new_data = alloc(new_cap * sizeof<T>()) as &mut T;
    for let mut i = 0; i < first.len; i+=1 {
        new_data[i] = first.data[i];
    }
    for let mut i = 0; i < second.len; i+=1 {
        new_data[first.len + i] = second.data[i];
    }
    return {
        data=new_data,
        len=new_len,
        cap=new_cap
    };
}

def Sequence_reverse<T>(s: Sequence<T>): Sequence<T> {
    for let mut i = 0; i < s.len / 2; i+=1 {
        let tmp = s.data[i];
        s.data[i] = s.data[s.len - i - 1];
        s.data[s.len - i - 1] = tmp;
    }
    return s;
}

def Sequence_drop<T>(s: &Sequence<T>) {
    if s->data != Null {
        del s->data;
    }
}

def Sequence_nth<T>(s: &Sequence<T>, n: Int): Option<&T> {
    if (n < 0 || n >= s->len) {
        return Option<&T> of Nothing;
    } else {
        return Option<&T> of Some(&(s->data[n]));
    }
}

def test<T>(first: &T, second: &T) {
    if let (of Some(a), of Some(b)) = (Sequence_from<T>(first, 5), Sequence_from<T>(second, 5)) {
        let seq = Sequence_concat<T>(a, Sequence_reverse<T>(b));
        Sequence_println<T>(&seq);
        Sequence_drop<T>(&a);
        Sequence_drop<T>(&b);
        Sequence_drop<T>(&seq);
    }
}

let first = [1, 2, 3, 4, 5];
let second = [6, 7, 8, 9, 10];
print("Sequence of ints: ");
test<Int>(&first as &Int, &second as &Int);

let raw_text = "Hello, world!\\0";
if let of Some(str) = String_from(&raw_text as &Char) {
    let first = [
        str,
        String_push(String_clone(&str), '!'),
        String_push(String_push(String_clone(&str), '!'), '!'),
        String_push(String_push(String_push(String_clone(&str), '!'), '!'), '!'),
        String_push(String_push(String_push(String_push(String_clone(&str), '!'), '!'), '!'), '!')
    ];

    if let of Some(mut second) = Sequence_from<String>(&first as &String, sizeof(first) / sizeof<String>()) {
        print("Sequence of strings: ");
        test<String>(&first as &String, second.data);
        for let mut i = 0; i < 5; i+=1 {
            print(i, ": "); String_print(&(first[i])); print("\\n");
        }
        second = Sequence_reverse<String>(second);
        for let mut i = 0; i < 5; i+=1 {
            if let of Some(s) = Sequence_nth<String>(&second, i) {
                print(i + 5, ": "); String_print(s); print("\\n");
            	String_drop(s);
            }
        }
    	Sequence_drop<String>(&second);
    }
}
print("Vector operations done!\\n");`);
                    break;

                case "calculator":
                    document.getElementById("input").value = "(4 + 6) * ((8 - 3) / 2) + (9 % 5)";
                    source.setValue(`// A mathematical expression for the calculator to evaluate.
enum Expr {
    // Addition of two sub-expressions.
    Add (&Expr, &Expr),
    // Subtraction of two sub-expressions.
    Sub (&Expr, &Expr),
    // Multiplication of two sub-expressions.
    Mul (&Expr, &Expr),
    // Division of two sub-expressions.
    Div (&Expr, &Expr),
    // Remainder of two sub-expressions.
    Rem (&Expr, &Expr),
    // A number.
    Num Float,
    // Parenthesized sub-expression.
    Group &Expr,
}

// The current input to the parser.
struct Input {
    // The start of the entire input.
    start: &Char,
    // The current location in the input.
    loc: Int,
    // The length of the input.
    length: Int
}

// The result of parsing an expression.
enum ParseResult {
    // A successful parse.
    // This contains the remaining input and the parsed expression.
    Ok (Input, &Expr),
    // An error while parsing.
    // This contains the location of the error.
    Err Int
}

// Main code of the calculator.
def main() {
    // Read the input.
    let input = read();
    // Parse the input.
    match parse_expr(input) {
        // If the parse was successful, evaluate the expression.
        of Ok (input, n) => {
            // Print the expression.
            print_expr(n);
            // Print the result of the evaluation.
            print("\\n => ", eval(n), "\\n");
            // Free the expression and the input.
            free_expr(n);
            free_input(input);
        },
        // If the parse failed, print an error message.
        of Err n => print("\\nCalculator: error while parsing at character: \\n", input.start[n], "\\n")
    }
}

def eval(expr: &Expr): Float = match *expr {
    of Add (lhs, rhs) => eval(lhs) + eval(rhs),
    of Sub (lhs, rhs) => eval(lhs) - eval(rhs),
    of Mul (lhs, rhs) => eval(lhs) * eval(rhs),
    of Div (lhs, rhs) => eval(lhs) / eval(rhs),
    of Rem (lhs, rhs) => eval(lhs) % eval(rhs),
    of Num n => n,
    of Group inner => eval(inner)
};

def print_expr(expr: &Expr) {
    match *expr {
        of Add(lhs, rhs) => {
            print_expr(lhs);
            print(" + ");
            print_expr(rhs);
        },
        of Sub(lhs, rhs) => {
            print_expr(lhs);
            print(" - ");
            print_expr(rhs);
        },
        of Mul(lhs, rhs) => {
            print_expr(lhs);
            print(" * ");
            print_expr(rhs);
        },
        of Div(lhs, rhs) => {
            print_expr(lhs);
            print(" / ");
            print_expr(rhs);
        },
        of Rem(lhs, rhs) => {
            print_expr(lhs);
            print(" % ");
            print_expr(rhs);
        },
        of Num n => print(n),
        of Group expr => {
            print("(");
            print_expr(expr);
            print(")");
        }
    }
}

def free_expr(expr: &Expr) {
    match *expr {
        of Add (lhs, rhs)
        | of Sub (lhs, rhs)
        | of Mul (lhs, rhs)
        | of Div (lhs, rhs)
        | of Rem (lhs, rhs) => {
            free_expr(lhs);
            free_expr(rhs);
        },
        of Group inner => free_expr(inner),
        _ => {}
    }
    del expr;
}

def free_input(input: Input) {
    del input.start;
}

def is_between_inclusive(ch: Char, start: Char, end: Char): Bool {
    let start = start as Int,
        end = end as Int,
        ch = ch as Int;

    return start <= ch && ch <= end;
}

def parse_float(mut input: Input): ParseResult {
    let save = input;
    let mut n = 0.0;

    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    if !(is_between_inclusive(input.start[input.loc], '0', '9')) {
        return ParseResult of Err (input.loc);
    }

    for (); input.loc < input.length && is_between_inclusive(input.start[input.loc], '0', '9'); input.loc += 1 {
        n *= 10.0;
        n += (input.start[input.loc] as Int - '0' as Int) as Float;
    }

    if (input.loc < input.length && input.start[input.loc] == '.') {
        input.loc += 1;
        let mut m = 0.1;
        for (); input.loc < input.length && is_between_inclusive(input.start[input.loc], '0', '9'); input.loc += 1 {
            n += (input.start[input.loc] as Int - '0' as Int) as Float * m;
            m *= 0.1;
        }
    }
    return ParseResult of Ok (input, new Expr of Num n);
}

def binop(
    lhs: &Expr,
    mut input: Input,
    op: Char,
    factor: Input -> ParseResult,
    cons: (&Expr, &Expr) -> &Expr): ParseResult {

    input = parse_whitespaces(input);
    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    if input.start[input.loc] == op {
        input.loc += 1;
        input = parse_whitespaces(input);
        match factor(input) {
            of Ok (input, rhs) => {
                return ParseResult of Ok (input, cons(lhs, rhs));
            },
            of Err _ => {
                return ParseResult of Err (input.loc);
            }
        }
    } else {
        return ParseResult of Err (input.loc);
    }
}

def parse_expr(mut input: Input): ParseResult {
    let save = input;

    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    def add_cons(lhs: &Expr, rhs: &Expr): &Expr {
        return new Expr of Add(lhs, rhs);
    } 
    def sub_cons(lhs: &Expr, rhs: &Expr): &Expr {
        return new Expr of Sub(lhs, rhs);
    }

    match parse_term(input) {
        of Ok (new_input, mut lhs) => {
            input = new_input;
            while True {
                lhs = match binop(lhs, input, '+', parse_term, add_cons) {
                    of Ok (new_input, new_lhs) => {
                        input = new_input;
                        new_lhs;
                    },
                    of Err _ => match binop(lhs, input, '-', parse_term, sub_cons) {
                        of Ok (new_input, new_lhs) => {
                            input = new_input;
                            new_lhs;
                        },
                        of Err _ => {
                            return ParseResult of Ok (input, lhs);
                        }
                    }
                };
            }
            return ParseResult of Ok (input, lhs);
        },
        of Err _ => {return ParseResult of Err (input.loc);}
    }
}

def parse_term(mut input: Input): ParseResult {
    let save = input;

    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    def mul_cons(lhs: &Expr, rhs: &Expr): &Expr {
        return new Expr of Mul(lhs, rhs);
    } 
    def div_cons(lhs: &Expr, rhs: &Expr): &Expr {
        return new Expr of Div(lhs, rhs);
    }
    def rem_cons(lhs: &Expr, rhs: &Expr): &Expr {
        return new Expr of Rem(lhs, rhs);
    }

    match parse_atom(input) {
        of Ok (new_input, mut lhs) => {
            input = new_input;
            while True {
                lhs = match binop(lhs, input, '*', parse_atom, mul_cons) {
                    of Ok (new_input, new_lhs) => {
                        input = new_input;
                        new_lhs;
                    },
                    of Err _ => match binop(lhs, input, '/', parse_atom, div_cons) {
                        of Ok (new_input, new_lhs) => {
                            input = new_input;
                            new_lhs;
                        },
                        of Err _ => match binop(lhs, input, '%', parse_atom, rem_cons) {
                            of Ok (new_input, new_lhs) => {
                                input = new_input;
                                new_lhs;
                            },
                            of Err _ => {
                                return ParseResult of Ok (input, lhs);
                            }
                        }
                    }
                };
            }
            return ParseResult of Ok (input, lhs);
        },
        of Err _ => {return ParseResult of Err (input.loc);}
    }
}


def parse_atom(input: Input): ParseResult {
    let save = input;

    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    let result = parse_float(input);
    if let of Err _ = result {
        return parse_group(input);
    }
    return result;
}

def parse_group(mut input: Input): ParseResult {
    let save = input;

    if (input.loc == input.length) {
        return ParseResult of Err (input.loc);
    }

    if (input.start[input.loc] != '(') {
        return ParseResult of Err (input.loc);
    }

    input.loc += 1;
    let input = parse_whitespaces(input);

    return match parse_expr(input) {
        of Ok (mut input, expr) => {
            input = parse_whitespaces(input);
            if (input.start[input.loc] != ')') {
                return ParseResult of Err (input.loc);
            }
            input.loc += 1;
            ParseResult of Ok (input, new Expr of Group(expr));
        },
        of Err _ => ParseResult of Err (input.loc)
    };
}

def parse_whitespaces(mut input: Input): Input {
    let save = input;

    if (input.loc == input.length) {
        return input;
    }

    for (); input.loc < input.length && (input.start[input.loc] == ' ' || input.start[input.loc] == '\\r' || input.start[input.loc] == '\\n'); input.loc += 1 {}

    return input;
}

def is_done(input: Input): Bool = input.loc >= input.length;

def strlen(ch: &Char): Int {
    let mut i = 0;
    for (); ch[i] != '\\0'; i+=1 {}
    return i;
}

def getchar(): Char {
    let mut ch = ' ';
    input(&mut ch);
    return ch;
}

def readline(ch: &mut Char, len: Int): Int {
    let mut i = len;

    let mut c = getchar();
    for (); c != '\\n' && c != '\\0'; i+=1 {
        ch[i] = c;
        c = getchar();
    }
    return i;
}

def read(): Input {
    let buf = alloc(sizeof<Char>() * 1024) as &mut Char;

    print("? ");
    let len = readline(buf, 0);
    buf[len] = '\\0';

    return {
        start = buf as &Char,
        length = strlen(buf),
        loc = 0
    };
}
def test(): Input {
    let buf = alloc(sizeof<Char>() * 34) as &mut Char;
    *(buf as &mut [Char * 34]) = "(4 + 6) * ((8 - 3) / 2) + (9 % 5)\\0";
    
    return {
        start = buf as &Char,
        length = strlen(buf),
        loc = 0
    };
}

main();`);
                    break;
                case "tree":
                    source.setValue(`enum Option<T> {
    Some(T),
    Nothing
}

def print_option<T>(opt: Option<T>, printer: T -> None) {
	match opt {
        of Some(x) => {
            print("Some(");
            printer(x);
            print(")");
        },
        of Nothing => { print("Nothing"); }
    }
}

enum List<T> {
    Cons(T, &List<T>),
    Nil
}

def print_list<T>(list: &List<T>): Option<T> {
    match *list {
        of Cons(x, &of Nil) => {
            print(x);
            return Option<T> of Some(x);
        },
        of Cons(x, xs) => {
            print(x, ", ");
            let _ = print_list<T>(xs);
            return Option<T> of Some(x);
        },
        of Nil => Option<T> of Nothing
    }
}

enum Tree<T> {
    Node(T, &List<Tree<T>>),
    Leaf(T),
}

def print_tree_list<T>(list: &List<Tree<T>>): Option<T> {
    match *list {
        of Cons(x, &of Nil) => {
            return print_tree<T>(&x);
        },
        of Cons(x, xs) => {
            let _ = print_tree<T>(&x);
            print(", ");
            return print_tree_list<T>(xs);
        },
        of Nil => Option<T> of Nothing
    }
}

def print_tree<T>(tree: &Tree<T>): Option<T> {
    match *tree {
        of Node(x, &of Nil) => {
            print(x);
            return Option<T> of Some(x);
        },
        of Node(x, xs) => {
            print(x, ", ");
            return print_tree_list<T>(xs);
        },
        of Leaf n => {
            print(n);
            return Option<T> of Some(n);
        }
    }
}

def test<T>(tree: &Tree<T>, printer: T -> None) {
    let result = print_tree<T>(tree);
    print("\\n");
    print_option<T>(result, printer);
}

let x = Tree<Int> of Node(
    1,
    new List<Tree<Int>> of Cons(
        Tree<Int> of Leaf(2),
        new List<Tree<Int>> of Cons(
            Tree<Int> of Leaf(3),
            new List<Tree<Int>> of Nil
        )
    )
);

let y = Tree<Char> of Node(
    'a',
    new List<Tree<Char>> of Cons(
        Tree<Char> of Leaf('b'),
        new List<Tree<Char>> of Cons(
            Tree<Char> of Leaf('c'),
            new List<Tree<Char>> of Nil
        )
    )
);
def print_int(x: Int) = print(x);
def print_char(x: Char) = print(x);
test<Int>(&x, print_int);
print("\\n");
test<Char>(&y, print_char);
print("\\n");
`);
                    break;
                case "lisp":
                    source.setValue(`// Implement a scheme evaluator in Sage.
def strlen(ch: &Char): Int {
    let mut len = 0;
    while ch[len] != '\\0' {
        len += 1;
    }
    return len;
}

def strcpy(dst: &mut Char, src: &Char) {
    let mut i = 0;
    for (); src[i] != '\\0'; i+=1 {
        dst[i] = src[i];
    }
    dst[i] = '\\0';
}

struct String {
    data: &mut Char,
    len: Int,
}

def string_new(s: &Char): String {
    let mut len = 0;
    while s[len] != '\\0' {
        len += 1;
    }

    let buffer = alloc(sizeof<Char>() * len) as &mut Char;
    strcpy(buffer, s);

    return {
        data = buffer,
        len = len
    };
}

def string_print(s: String) {
    for let mut i=0; i < s.len; i+=1 {
        print(s.data[i]);
    }
}

enum Expr {
    Boolean Bool,
    Decimal Float,
    Integer Int,
    Complex {
        real: Float,
        imag: Float,
    },
    Symbol String,
    String String,
    Pair {
        car: &Expr,
        cdr: &Expr,
    },
    Nil,
    defedure {
        params: &Expr,
        body: &Expr,
        env: &Env,
    },
    Closure {
        params: &Expr,
        body: &Expr,
        env: &Env,
    },
    Builtin(String, (&Expr, &Env) -> Expr),
    Continuation {
        env: &Env,
    },
}

struct Env {
    bindings: Expr,
    parent: &Env,
}

def root_env(): Env = {
    bindings = Expr of Nil,
    parent = Null
};

def env_free(env: Env) {
    expr_free(env.bindings);
}

def expr_free(expr: Expr) {
    match expr {
        of Pair { car, cdr } => {
            expr_free(*car);
            expr_free(*cdr);
            del car;
            del cdr;
        },
        of defedure { params, body, env } => {
            expr_free(*params);
            expr_free(*body);
            env_free(*env);
            del params;
            del body;
            del env;
        },
        of Closure { params, body, env } => {
            expr_free(*params);
            expr_free(*body);
            env_free(*env);
            del params;
            del body;
            del env;
        },
        of Continuation { env } => {
            env_free(*env);
            del env;
        },
        _ => {}
    }
}

def cons(car: Expr, cdr: Expr): Expr {
    return Expr of Pair {
        car = new car,
        cdr = new cdr
    };
}

def expr_print(expr: &Expr) {
    match *expr {
        of Boolean b => print(b),
        of Integer i => print(i),
        of Symbol s => print(s),
        of String s => print(s),
        of Pair { car, cdr } => {
            print("(");
            expr_print(car);
            print(" . ");
            expr_print(cdr);
            print(")");
        },
        of Nil => print("()"),
        of defedure { params, body, env } => {
            print("#<defedure>");
        },
        of Closure { params, body, env } => {
            print("#<closure>");
        },
        of Builtin b => {
            print("#<builtin>");
        },
        of Continuation { env } => {
            print("#<continuation>");
        },
        _ => {}
    }
}

def expr_println(expr: &Expr) {
    expr_print(expr);
    print("\\n");
}

def expr_reduce(mut acc: Expr, x: Expr, env: &Env, fun: (Expr, Expr, &Env) -> Expr): Expr {
    match x {
        of Pair { car, cdr } => {
            acc = fun(acc, *car, env);
            expr_reduce(acc, *cdr, env, fun);
        },
        of Nil => acc,
        _ => fun(acc, x, env)
    }
}

def expr_map(x: Expr, env: &Env, fun: (Expr, &Env) -> Expr): Expr {
    match x {
        of Pair { car, cdr } => cons(fun(*car, env), expr_map(*cdr, env, fun)),
        of Nil => Expr of Nil,
        _ => fun(x, env)
    }
}

def sum(acc: Expr, x: Expr, env: &Env): Expr {
    match acc {
        of Integer i => {
            match x {
                of Integer j => Expr of Integer (i + j),
                of Decimal j => Expr of Decimal (i as Float + j),
                _ => acc
            }
        },
        of Decimal i => {
            match x {
                of Integer j => Expr of Decimal (i + j as Float),
                of Decimal j => Expr of Decimal (i + j),
                _ => acc
            }
        },
        _ => acc
    }
}

def prod(acc: Expr, x: Expr, env: &Env): Expr {
    match acc {
        of Integer i => {
            match x {
                of Integer j => Expr of Integer (i * j),
                of Decimal j => Expr of Decimal (i as Float * j),
                _ => acc
            }
        },
        of Decimal i => {
            match x {
                of Integer j => Expr of Decimal (i * j as Float),
                of Decimal j => Expr of Decimal (i * j),
                _ => acc
            }
        },
        _ => acc
    }
}

def inc(x: Expr, env: &Env): Expr {
    match x {
        of Integer i => Expr of Integer (i + 1),
        of Decimal i => Expr of Decimal (i + 1.0),
        _ => x
    }
}

def dec(x: Expr, env: &Env): Expr {
    match x {
        of Integer i => Expr of Integer (i - 1),
        of Decimal i => Expr of Decimal (i - 1.0),
        _ => x
    }
}


def range(start: Int, end: Int): Expr {
    if start > end {
        return Expr of Nil;
    } else {
        return cons(Expr of Integer start, range(start + 1, end));
    }
}

let expr = cons(Expr of Integer 1, cons(Expr of Integer 2, cons(Expr of Integer 3, cons(Expr of Integer 4, Expr of Nil))));
let env = root_env();

print("expr: ");
expr_println(&expr);

print("  reduce(sum): ");
let result = expr_reduce(Expr of Integer 0, expr, &env, sum);
expr_println(&result);

print("  reduce(product): ");
let result = expr_reduce(Expr of Integer 1, expr, &env, prod);
expr_println(&result);

print("  map(increment): ");
let result = expr_map(expr, &env, inc);
expr_println(&result);

for let mut i=0; i<20; i+=1 {
    let range = range(1, i);
    expr_println(&range);
}

let text = "woo!!!\\0";
let s = string_new(&text as &Char);
string_print(s);
`);
                    break;
                case "alloc":
                    source.setValue(`enum Option<T> {
    Some(T),
    Nothing
}

def allocate<T>(count: Int): Option<&mut T> {
    if (count <= 0) {
        return Option<&mut T> of Nothing;
    }
    let ptr = alloc(count * sizeof<T>()) as &mut T;
    if (ptr == Null) {
        return Option<&mut T> of Nothing;
    } else {
        return Option<&mut T> of Some(ptr);
    }
}

struct Point<T> {
    x: T,
    y: T
}

match allocate<Point<Int>>(5) {
    of Some(val) => {
        for let mut i=0; i<5; i+=1 {
            val[i].x = i;
            val[i].y = i * 2;
        }
        print("Allocated 5 items: ", *(val as &[{x: Int, y: Int} * 5]), "\\n");
        print("                 : ", *(val as &[Int * 10]), "\\n");
    },
    of Nothing => {
        print("Failed to allocate memory");
    }
}`);
                    break;
                case "match":
                    source.setValue(`enum Direction {
    North, South, East, West
}

match (Direction of South, 2, struct {x = 5, y = -6}) {
    (of North, _, _)
    | (of East, _, _)
    | (of West, _, _)
    | (of South, 3, _) => print("Incorrect!\\n"),
    (of South, 2, {x = 5, y = -6}) => {
        print("Correct!\\n");
    },
    _ => print("Incorrect!\\n")
}

enum Option<T> {
    Some(T),
    Nothing
}

def divide(n: Int, d: Int): Option<Int> {
    if (d == 0) {
        return Option<Int> of Nothing;
    } else {
        return Option<Int> of Some(n / d);
    }
}

if let of Some(n) = divide(5, 0) {
    print("5 / 0 = ", n, "\\n");
} else {
    print("5 / 0 = undefined\\n");
}`);
                    document.getElementById("input").value = "";
                    break;
                case "cat":
                    source.setValue(`let mut ch = ' ';
while (ch != '\\0') {
    input(&mut ch);
    print(ch);
}`);
                    document.getElementById("input").value = "this is a cat program!";
                    break;
                case "factorial":
                    source.setValue(`def fact(n: Int): Int {
    match n {
        0 | 1 => 1,
        _ => n * fact(n - 1)
    }
}

let mut n = 0;
input(&mut n);

print(fact(n));`);
                    document.getElementById("input").value = "5";
                    break;
                case "ffi":
                    source.setValue(`// NOTE: I have *explicitly* given the VM access to the JavaScript functions 
// \`alert\` and \`eval\` internally. If you try and call any other
// foreign function, the VM will not have bindings for them and will halt.
// Running someone else's code in a safe manner is as simple as making
// sure you only provide bindings for safe foreign functions to execute!
// (e.g.\`eval\` is surely not safe, but something like \`alert\` is generally safe.)

extern alert(message: &Char);
extern eval(code: &mut Char, size: Int);

const BUFFER_SIZE = 1024;

def read(buf: &mut Char) {
    let mut ch = ' ';
    input(&mut ch);
    for let mut i = 0; ch; i+=1 {
        buf[i] = ch;
        input(&mut ch);
    }
}

def write(buf: &Char) {
    for let mut i = 0; buf[i]; i+=1 {
        print(buf[i]);
    }
}

let buf = alloc(BUFFER_SIZE) as &mut Char;

// Read the code to evaluate from the input box!
read(buf);
print("Input code: ");
write(buf);

// Evaluate the code in the input box!
eval(buf, BUFFER_SIZE);

// Print the result of the evaluation to the output box!
print("\\nResult: ");
write(buf);

// Call the JavaScript alert function!
alert(buf);`);
                    document.getElementById("input").value = "('b' + 'a' + + 'a' + 'a').toUpperCase()";
                    break;
                case "hello world":
                    source.setValue(`print("Hello, world!\\n");`);
                    document.getElementById("input").value = "";
                    break;
                default:
                    console.log("Unknown example: " + example);
                    break;
            }
        }
        setExampleProgram();
        document.getElementById("example").addEventListener("change", () => {
            setExampleProgram();
            document.getElementById("source").value = source.getValue();
            source.refresh();
            compile_and_run();
        });
        document.getElementById("target").addEventListener("change", () => {
            document.getElementById("source").value = source.getValue();
            source.refresh();
            compile_and_run();
        });
        document.getElementById("run").addEventListener("click", compile_and_run, false);
        </script>
        <select id="example" style="height: 3em; float: left;">
            <option value="hello world">Click to select example program (Default Hello World)</option>
            <option value="hex-editor">Hex Editor</option>
            <option value="calculator">Calculator (Parser)</option>
            <option value="ffi">Foreign Function Interface (JavaScript Example)</option>
            <option value="hashmap">HashMap (Generics)</option>
            <option value="vector">Vector (Generics)</option>
            <option value="aes">AES (128, 192, 256 bit (en/de)crypt)</option>
            <option value="alloc">Allocator</option>
            <option value="result">Result Enum (Generics)</option>
            <option value="list">Linked List Enum (Generics)</option>
            <option value="lisp">S-Expressions</option>
            <option value="tree">Tree (Generics)</option>
            <option value="match">Pattern Matching</option>
            <option value="cat">Cat</option>
            <option value="factorial">Factorial</option>
        </select>
        <div class="row">
            <div class="col"><textarea id="source"></textarea></div>
            <div class="col" style="height: 20em;">
                <textarea style="overflow: auto; background-color: black; color: white;" id="output"></textarea>
            </div>
        </div>
        <div>
            <textarea id="input" style="resize: none; float: left;" cols="10">Input Text</textarea>
            <select id="target" style="height: 3em; float: left;">
                <option value="run"><b>Click to see stages of output</b></option>
                <option value="run">Execution Output</option>
                <option value="lir">Lower IR</option>
                <option value="asm">Assembly IR</option>
                <option value="vm">Virtual Machine Code</option>
                <option value="c">C Source</option>
                <option value="x86">x86 Assembly</option>
            </select>
            <button id="run" style="height: 3em; float: left;">Compile</button>
        </div>
        <script>
            compile_and_run();
        </script>
    </body>
</html>